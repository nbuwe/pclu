% Copyright Massachusetts Institute of Technology 1980, 1989

unify_spacing = proc (s: string) returns (string)
	ac = array[char]
	own buf: ac := ac$predict(1, 100)
	ac$trim(buf, 1, 0)
	ever: bool := false
	out: bool := false
	for c: char in string$chars(s) do
		if c = '\t'
		   then ever := true
			if out
			   then ac$addh(buf, ' ')
				out := false
			   end
			continue
		elseif c = ' '
		   then if out
			   then ac$addh(buf, ' ')
				out := false
				continue
			   end
			ever := true
			continue
		end
		ac$addh(buf, c)
		out := true
		end
	if ever
	   then if ac$top(buf) = ' '
		   then ac$remh(buf) end
		   except when bounds: end
		s := string$ac2s(buf)
	   end
	ac$trim(buf, 1, 0)
	return(s)
	end unify_spacing
