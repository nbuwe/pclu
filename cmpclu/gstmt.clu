
%  Copyright    Massachusetts Institute of Technology     1990,1991

% omit decls, only do assignment part of declinits
% similarly for tag, for, except

g_stmt = proc (e: g_env, s: stmt)
    if debug_lineno then
       logit(800, "\n/**** " || int$unparse(s.line) || " ****/\n") end
    e.lineno := s.line
    if debug_stmt then stmt_print(s) end
    tv$enter()
    tagcase s.abs
       tag decl (sa: decl):
	   %	   g_decl(e, sa)
	   if dbg_info$active() then
	      g_decl_debug(e, sa)
	      end
       tag declinit (sa: declinit):
	   g_declinit(e, sa)
       tag assn (sa: assn):
	   g_assn (e, sa)
       tag invoke (sa: invoke):
	   g_sinvoke(e, sa)
       tag while_ (sa: whilestmt):
	   g_while(e, sa)
       tag for_ (sa: forstmt):
	   g_for(e, sa)
       tag if_ (sa: ifstmt):
	   g_if (e, sa)
       tag tag_ (sa: tagstmt):
	   g_tagcase(e, sa)
       tag return_ (sa: exprlist):
	   g_return(e, sa)
       tag yield_ (sa: exprlist):
	   g_yield(e, sa)
       tag signal_ (sa: condition):
	   g_signal(e, sa)
       tag exit_ (sa: condition):
	   g_exit(e, sa)
       tag break_:
	   g_break(e)
       tag continue_:
	   g_continue(e)
       tag body (sa: body):
	   g_beginend(e, sa)
       tag except_ (sa: exceptstmt):
	   g_except(e, sa)
       tag resignal_ (sa: resignalstmt):
	   g_resignal(e, sa)
       others:
       end
    tv$leave()
    g_env$newline(e)
    end g_stmt

g_decllist = proc (e: g_env, decls: decllist)

    for each_d: decl in decllist$elements(decls) do
	g_decl(e, each_d)
	g_env$newline(e)
	end
    end g_decllist

g_decl = proc (e: g_env, stmnt: decl)

    for each_i: idn in idnlist$elements(stmnt.idns) do
	g_decl_typespec(e, stmnt.type_, idnlit(each_i))
	end
    end g_decl

g_decl_debug = proc (e: g_env, stmnt: decl)

    for each_i: idn in idnlist$elements(stmnt.idns) do
	g_idn_plain(e, each_i)
	g_env$putl(e, ".num = UNINIT;")
	end
    end g_decl_debug

g_declinit = proc (e: g_env, stmnt: declinit)

    %% stmnt.idns : stmnt.type_ := stmnt.expr

    g_env$puts(e, "{")
    g_env$indent(e)
    g_env$newline(e)
    g_env$puts(e, "{")
    g_expr0(e, stmnt.expr)
    el: exprlist := g_expr1(e, stmnt.expr)
    i: int := 1
    for d:decl in decllist$elements(stmnt.decls) do
	for each_idn:idn in idnlist$elements(d.idns) do
	    g_c_assn(e, each_idn, el[i])
	    % g_idn(e, each_idn)
	    % g_env$puts(e, " = ")
	    % g_expr2(e, el[i])
	    % g_env$putl(e, ";")
	    i := i + 1
	    end % for
	end % for
    g_anys(e, stmnt.anys)
    g_env$putl(e, "}")
    g_env$putl(e, "}")
    g_env$outdent(e)
    end g_declinit

g_assn = proc (e: g_env, stmnt: assn)

    %% stmnt.left := stmnt.right

    g_env$putl(e, "{")

    %   iterate over elements of right hand side, generating temp defns
    for each_e: expr in exprlist$elements(stmnt.right) do
	g_expr0(e, each_e)
	end % for

    %   iterate over lhs looking for idns that will get clobbered by
    %		assignment statements and declare appropriate temps,
    %		to save the values, and save a list of the temps for
    %		later use
    rsize: int := exprlist$size(stmnt.right)
    altlist: mexprlist := mexprlist$new()
    for ind: int in idnlist$indexes(stmnt.left) do
	for ind2: int in int$from_to(ind, rsize) do
	    tagcase stmnt.right[ind2].abs
	       tag idn (i:idn):
		   if idn$similar (stmnt.left[ind], i) then
		      %logit("g_assn: SWAPPING")
		      et: typespec := stmnt.right[ind2].typespec
		      tv$decl_next(e, et)
		      tvi: expr := tv$idn_next(et)
		      altlist := mexprlist$addh(altlist, mexpr$make_one(tvi))
		      else
			   altlist := mexprlist$addh(altlist, mexpr$make_none(nil))
		      end % if
	       others:
		   altlist := mexprlist$addh(altlist, mexpr$make_none(nil))
	       end % tag
	    end % for
	end % for

    for ind: int in mexprlist$indexes(altlist) do
	if mexpr$is_one(altlist[ind]) then
	   % g_expr2(e, mexpr$value_one(altlist[ind]))
	   g_expr_plain(e, mexpr$value_one(altlist[ind]))
	   g_env$puts(e, " = ")
	   ex: expr := stmnt.right[ind]
	   tagcase ex.abs
	      tag idn (i:idn):
		  g_idn_plain(e, i)
	      others:
	      end
	   g_env$putl(e, ";")
	   end
	end

    %   iterate over elements of right hand side, accumulating terminal exprs
    %	(1 per result value) and producing newright, 
    %   a list of terminal expressions
    newright: exprlist := exprlist$new()
    for each_e: expr in exprlist$elements(stmnt.right) do
	newright := seq_extend[expr](newright, g_expr1(e, each_e))
	end % for

    i: int := 1
    for each_idn: idn in idnlist$elements(stmnt.left) do
	% g_idn(e, each_idn)
	% g_env$puts(e, " = ")
	tagcase altlist[i]
	   tag none:
	       % g_expr2(e, newright[i])
	       g_c_assn(e, each_idn, newright[i])
	   tag one:
	       % g_expr2(e, mexpr$value_one(altlist[i]))
	       g_c_assn(e, each_idn, mexpr$value_one(altlist[i]))
	   end
	   except when bounds:
		       % g_expr2(e, newright[i])
		       g_c_assn(e, each_idn, newright[i])
		  end
	i := i + 1
	end % for
       except when bounds: end
    g_anys(e, stmnt.anys)
    g_env$putl(e, "}")
    end g_assn

g_sinvoke = proc (e: g_env, stmnt: invoke)
    g_env$putl(e, "{")
    exp: expr := expr$create(exprabs$make_invoke(stmnt), stmnt.apply.typespec)
    g_expr0(e, exp)
    g_einvoke(e, stmnt)
    g_env$putl(e, "}")
    end g_sinvoke

g_temp_decl = proc (e: g_env, n: int)
    return
    end g_temp_decl


% helper for emitting a bunch of re-signals
g_propagate = proc (e: g_env, sig_name: string)
    g_env$puts(e, "if (")
    g_err(e)
    g_env$puts(e, " == ")
    g_env$puts(e, sig_name)
    g_env$putl(e, ")")
    g_env$indent(e)
    g_env$puts(e, "signal(")
    g_env$puts(e, sig_name)
    g_env$putl(e, ");")
    g_env$outdent(e)
    end g_propagate


g_while = proc (e: g_env, stmnt: whilestmt)

    %% WHILE ( stmnt.test ) DO
    %%	  stmnt.body
    %% END

    loop_context$enter_while()
    g_env$putl(e, "for (;;) { /* while */")
    g_env$indent(e)

    g_expr0(e, stmnt.test)
    el: exprlist := g_expr1(e, stmnt.test)
    g_env$puts(e, "if (")
    g_expr2(e, exprlist$bottom(el))
    g_env$putl(e, " != true)")
    g_env$indent(e)
    g_env$putl(e, "break;")
    g_env$outdent(e)

    g_body(e, stmnt.body)

    g_env$outdent(e)
    g_env$putl(e, "}")
    n: int := loop_context$get_current_while_count()
    g_env$putl(e, "end_while_" || int$unparse(n) || ":")
    g_env$putl(e, "__CLU_END_LABEL;")	 % only used by "break"
    loop_context$leave()
    end g_while


g_for = proc (e: g_env, stmnt: forstmt)

    if g_inline_iter(e, stmnt) then return end
    g_env$putl(e, "{")
    g_env$indent(e)
    for each_arg: expr in exprlist$elements(stmnt.call.args) do
	g_expr_arg0(e, each_arg)
	end % for
    if ~g_qinv_test(stmnt.call.apply) then
       g_inv0(e, stmnt.call.apply)
       end

    % generate the call similarly to g_einvoke

    el: exprlist := exprlist$new()
    for each_arg: expr in exprlist$elements(stmnt.call.args) do
	new_el: exprlist := g_expr_arg1(e, each_arg)
	el := seq_extend[expr](el, new_el)
	end % for

    % compute proctype to be invoked

    quick: bool := g_qinv_test(stmnt.call.apply)
    proc_id: expr := stmnt.call.apply

    % generate the actual invocation
    ibname: name := ib$next_name()
    iterp: bool := current_apply$is_iter()
    if iterp then g_locals(e) g_env$putl(e, "body_ctrl_req = false;") end
    if quick
       then
	    g_qiinv(e, proc_id, el, ibname)
       else
	    gen_proc_id: expr := exprlist$bottom(g_inv1(e, stmnt.call.apply))
	    g_inv(e, gen_proc_id, proc_id)
	    g_env$puts(e, "(")
	    for each_arg: expr in exprlist$elements(el) do
		g_expr_arg(e, each_arg)
		g_env$puts(e, ", ")
		end % for

	    g_env$puts(e, ibname)
	    if ib$bodies()
	       then
		    g_env$puts(e, ", locals, iecode")
	       else
		    g_env$puts(e, ", &locals, &locals.ecode2")
	       end
	    g_env$putl(e, ");")
       end

    % Generate code to process end of iterator invocation:
    %	i.e., the end of the for statement (bodies generated elsewhere)

    if ib$bodies() then

       g_env$puts(e, "if (")
       g_err(e)
       g_env$putl(e, " != ERR_ok) {")

       if ~iterp then % an ITERBODY of a PROC
	  g_env$puts(e, "if (")
	  g_err(e)
	  g_env$putl(e, " == ERR_iterbodyreturn) {signal(ERR_iterbodyreturn);}")
	  g_env$puts(e, "if (")
	  g_err(e)
	  g_env$putl(e, " == ERR_iterbodysignal) {signal(ERR_iterbodysignal);}")
	  % 7/1/93: added resignalling for break
	  % 9/10/93: nope: convert break to ok here
	  g_env$puts(e, "if (")
	  g_err(e)
	  g_env$puts(e, " == ERR_break) ")
	  g_err(e)
	  g_env$putl(e, " = ERR_ok;")
	  g_env$puts(e, "if (")
	  g_err(e)
	  g_env$putl(e, " == ERR_iterbodyexit)")
	  g_err(e)
	  g_env$putl(e, " = *iecode;")	% * new 2/26/90 (_a_f_n_)
	  g_err_handle(e)
	  g_env$putl(e, "}")
	  else          % an ITERBODY of an ITER
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iterbodyreturn) {signal(ERR_iterbodyreturn);}")
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iterbodysignal) {signal(ERR_iterbodysignal);}")
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iterbodyexit) {signal(ERR_iterbodyexit);}")
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iteriterbodyreturn) {signal(ERR_iteriterbodyreturn);}")
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iteriterbodysignal) {signal(ERR_iteriterbodysignal);}")
	       % 7/1/93: added resignalling for break % 9/8/93 as long as no bcr
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$puts(e, " == ERR_break && !")
	       g_locals(e)
	       g_env$putl(e, "body_ctrl_req) {signal(ERR_break);}")
	       % 3/3/94: added resignalling for iibe as long as no bcr
	       % 	this fix supersedes 2/7/94 fix
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$puts(e, " == ERR_iteriterbodyexit && !")
	       g_locals(e)
	       g_env$putl(e, "body_ctrl_req) {signal(ERR_iteriterbodyexit);}")
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$puts(e, " == ERR_break) ")
	       g_err(e)
	       g_env$putl(e, " = ERR_ok;")
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iteriterbodyexit)")
	       g_err(e)
	       g_env$putl(e, " = *iecode;")	% * new 2/26/90 (_a_f_n_)
	       g_err_handle(e)
	       g_env$putl(e, "}")
	  end
       else	% (TOP-LEVEL) ITER OR PROC
	    if iterp then % ITER
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iterbodyreturn) {signal(ERR_iterbodyreturn);}")
	       g_env$puts(e, "if (")
	       g_err(e)
	       % added code to move locals.ecode2 to iecode 6/5/90
	       g_env$putl(e, " == ERR_iterbodysignal) {")
	       g_env$indent(e)
	       g_env$putl(e, "*iecode = locals.ecode2; signal(ERR_iterbodysignal);}")
	       g_env$outdent(e)
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iterbodyexit) {")
	       g_env$indent(e)
	       % added code to move locals.ecode2 to iecode 6/5/90
	       g_env$putl(e, "*iecode = locals.ecode2; signal(ERR_iterbodyexit);}")
	       g_env$outdent(e)

	       % preserved behavior in case this a body of this iter caused
	       % signal/return/exit - otherwise just propagate it 8/17/90
	       %if bcr
	       %clear bcr
	       g_env$puts(e, "if (")
	       g_locals(e)
	       g_env$putl(e, "body_ctrl_req == true) {")
	       g_env$indent(e)
	       g_locals(e)
	       g_env$putl(e, "body_ctrl_req = false;")
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iteriterbodyreturn) {signal(ERR_ok);}")
	       g_env$puts(e, "if (")
	       g_err(e)
	       % formerly *iecode 6/5/90
	       g_env$putl(e, " == ERR_iteriterbodysignal) {signal(locals.ecode2);}")
	       % 7/1/93: added break to ok conversion
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$puts(e, " == ERR_break) ")
	       g_err(e)
	       g_env$putl(e, " = ERR_ok;")   % 6/5/90 formerly *iecode
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$puts(e, " == ERR_iteriterbodyexit) ")
	       g_err(e)
	       g_env$putl(e, " = locals.ecode2;")   % 6/5/90 formerly *iecode
	       g_env$putl(e, "}")
	       g_env$outdent(e)
	       %else
	       %just propagate
	       g_env$putl(e, "else {")
	       g_env$indent(e)
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iteriterbodyreturn) {signal(ERR_iteriterbodyreturn);}")
	       % 7/1/93: added resignalling of break
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_break) {signal(ERR_break);}")
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iteriterbodysignal) {")
	       % added propagation of ecode2 to iecode 6/24/93
	       g_env$putl(e, "      *iecode = locals.ecode2; signal(ERR_iteriterbodysignal);}")
	       g_env$puts(e, "if (")
	       g_err(e)
	       g_env$putl(e, " == ERR_iteriterbodyexit) {")
	       % added propagation of ecode2 to iecode 6/24/93
	       g_env$puts(e, "      *iecode = locals.ecode2; signal(ERR_iteriterbodyexit);}")
	       g_env$putl(e, "}")
	       g_env$outdent(e)
	       %end
	       g_err_handle(e)
	       else % PROC
		    g_env$puts(e, "if (")
		    g_err(e)
		    g_env$putl(e, " == ERR_iterbodyreturn) {")
		    g_env$indent(e)
		    count: int := current_apply$get_val_count()
		    for i: int in int$from_to(1, count) do
			g_env$puts(e, "ret_" || int$unparse(i) || "->num = ")
			g_env$putl(e, "elist[" || int$unparse(i-1) || "].num;")
			% .ref new as of 2/27/90
			% ref to num 6/26/90
			end % for
		    g_env$putl(e, "signal(ERR_ok);")
		    g_env$outdent(e)
		    g_env$putl(e, "}")
		    g_env$puts(e, "if (")
		    g_err(e)
		    g_env$putl(e, " == ERR_iterbodysignal) {signal(locals.ecode2);}")
		    % 7/1/93: added break to ok conversion
		    g_env$puts(e, "if (")
		    g_err(e)
		    g_env$puts(e, " == ERR_break) ")
		    g_err(e)
		    g_env$putl(e, " = ERR_ok;")
		    g_env$puts(e, "if (")
		    g_err(e)
		    g_env$puts(e, " == ERR_iterbodyexit) ")
		    g_err(e)
		    g_env$putl(e, " = locals.ecode2;")
		    g_err_handle(e)
	       end
       end
    g_env$putl(e, "}")
    g_env$outdent(e)

    g_anys(e, stmnt.anys)
    g_env$newline(e)

    end g_for

g_if = proc (e: g_env, stmnt: ifstmt)

    %%  IF ( stmnt.arms[1].test ) THEN
    %%      stmnt.arms[1].body
    %%  ELSEIF ( stmnt.arms[2].test ) THEN
    %%      stmnt.arms[2].body
    %%   :
    %%  ELSEIF ( stmnt.arms[n].test ) THEN
    %%	   stmnt.arms[n].body
    %%  ELSE
    %%  	   stmnt.else_.body
    %%  END

    g_env$putl(e, "{ /* if */")

    % if
    g_expr0(e, stmnt.arms[1].test)
    el: exprlist := g_expr1(e, stmnt.arms[1].test)
    g_env$puts(e, "if (")
    g_expr2(e, exprlist$bottom(el))
    g_env$putl(e, " == true) { /* if */")

    % then
    g_env$indent(e)
    g_body(e, stmnt.arms[1].body)
    g_env$outdent(e)
    g_env$putl(e, "}")

    for i: int in int$from_to(2, ifarmlist$size(stmnt.arms)) do
        g_env$putl(e, "else {")
	e.lineno := stmnt.arms[i].line

	% elseif
	g_expr0(e, stmnt.arms[i].test)
	el := g_expr1(e, stmnt.arms[i].test)
        g_env$puts(e, "if (")
        g_expr2(e, exprlist$bottom(el))
        g_env$putl(e, " == true) { /* elseif */")

	% then
        g_env$indent(e)
        g_body(e, stmnt.arms[i].body)
        g_env$outdent(e)
        g_env$putl(e, "}")

	% NB: unclosed brace for "else" at the start
	% will close them all at once at the end
        end

    % else
    if mbody$is_body(stmnt.else_)
       then g_env$newline(e)
	    g_env$putl(e, "else { /* else */")

	    g_env$indent(e)
	    g_body(e, mbody$value_body(stmnt.else_))
	    g_env$outdent(e)
	    g_env$puts(e, "}")
       end

    % close elseif braces all at once
    for i:int in int$from_to(2, ifarmlist$size(stmnt.arms)) do
	g_env$puts(e, "}") end
    g_env$putl(e, "} /* end if */")
    end g_if


g_tagcase = proc (e: g_env, stmnt: tagstmt)

    %% TAGCASE stmnt.obj
    %%	   TAG stmnt.arms[1].tags ( stmnt.arms[1].var ) :
    %%		stmnt.arms[1].body
    %%	    :
    %%     OTHERS:
    %%		stmnt.others_.body
    %% END

    % NB: the parser doesn't eliminate the tagcase sugar (enabled by #extend)
    % so it is possible that stmnt.obj is not a oneof or variant.

    % If it *is* a oneof or variant, handle it quickly with a C switch
    if g_qtagcase(e, stmnt) then return end


    % In either case, this statement is equivalent to the following:
    %    % t is type_of(stmnt.obj)
    %	 % tagij is stmnt.arms[i].tags[j]
    %    IF t$is_tag11(stmnt.obj) COR ... COR t$is_tag1n(stmnt.obj) then
    %	     stmnt.arms[1].var := t$value_tag1(stmnt.obj)
    %        stmnt.arms[1].body
    %    ELSEIF t$is_tag21(stmnt.obj) COR...
    %     :
    %    ELSE
    %	     stmnt.others_.body
    %    END

    begin
	is_clutype: bool := false
	flist: fieldspeclist := fieldspeclist$new()
	type_prefix: string

	ty: typespec := stmnt.obj.typespec
	tagcase ty.abs
	   tag select (st: seltype):
	       flist := st.parms
	       type_prefix := st.gen.common || sep
	   tag cluster_ (ct: clutype):
	       is_clutype := true
	       type_prefix := ct.gen.common || sep
	   others:
	       logit(801, "tagcase: not select!?")
	       typespec_print(ty)
	   end

	g_env$puts(e, "do { /* tagcase")
	if is_clutype then g_env$puts(e, " sugar") end
	g_env$putl(e, " */")

	% declare temporary variables to evaluate the tagcase expression
	g_expr0(e, stmnt.obj)

	% temporary for the is_<tag> test result
	tv$decl_next(e, e.comp.bool_type)
	ttag: expr := tv$idn_next(e.comp.bool_type)

	% evaluate tagcase expression
	tobj: expr := exprlist$bottom(g_expr1(e, stmnt.obj))

	for each_arm: tagarm in tagarmlist$elements(stmnt.arms) do
	    % make it possible to break before the is_tag calls
	    e.lineno := each_arm.line

	    % list arm's tags in the block comment
	    g_env$puts(e, "{ /* ")
	    intro: string := "tag "
	    for each_tag: name in namelist$elements(each_arm.tags) do
		g_env$puts(e, intro)
		intro := ", "
		g_env$puts(e, each_tag)
		end
	    g_env$putl(e, " */")

	    % optional temporary for the tag value
	    tval: mexpr
	    tagcase each_arm.var
	       tag decl (d: decl):
		   tv$decl_next(e, d.type_)
		   tval := mexpr$make_one(tv$idn_next(d.type_))
	       others:
		   tval := mexpr$make_none(nil)
	       end

	    melse: string := ""
	    for each_tag: name in namelist$elements(each_arm.tags) do
		if string$empty(melse)
		   then melse := "else {"
		   else g_env$putl(e, melse) end

		% accessors: for selectors names are mapped to indices
		tag_suffix: string := each_tag
		if ~is_clutype then
		   % XXX: add prefix ("is_") just for field_index to strip it
		   _: string, idx: int := field_index("is_" || each_tag, flist)
		   tag_suffix := int$unparse(idx)

		   % comment for the cryptic numeric accessors
		   g_env$puts(e, "/* ")
		   g_env$puts(e, each_tag)
		   g_env$putl(e, " */")
		   end

		% typeOPis_tag(tobj, &ttag)
		g_err(e);
		g_env$puts(e, " = ");
		g_env$puts(e, type_prefix)
		g_env$puts(e, "is_")
		g_env$puts(e, tag_suffix)
		g_env$puts(e, "(");
		g_expr_arg(e, tobj)
		g_env$puts(e, ", &");
		g_expr_arg(e, ttag);
		g_env$putl(e, ");");
		g_err_handle(e);

		g_env$puts(e, "if (")
		g_expr_arg(e, ttag)	 % XXX
		g_env$putl(e, ".tf) {")
		g_env$indent(e)
		tagcase tval		 % do we have a variable?
		   tag one (dst: expr):  % fetch value into a temp
		       % typeOPvalue_tag(tobj, &dst)
		       g_err(e)
		       g_env$puts(e, " = ");
		       g_env$puts(e, type_prefix)
		       g_env$puts(e, "value_")
		       g_env$puts(e, tag_suffix)
		       g_env$puts(e, "(");
		       g_expr_arg(e, tobj)
		       g_env$puts(e, ", &");
		       g_expr_arg(e, dst);
		       g_env$putl(e, ");");
		       g_err_handle(e);
		   others:
		       g_env$putl(e, ";")   % stub
		   end % may be fetch value
		g_env$outdent(e)
		g_env$putl(e, "}")

		end % each_tag of this arm

	    % close outstanding else's
	    for i: int in int$from_to(2, namelist$size(each_arm.tags)) do
		g_env$puts(e, "}")
		end
	    g_env$ensure_newline(e)

	    % did we get a match on any of the tags in this arm?
	    g_env$puts(e, "if (")
	    g_expr_arg(e, ttag)		 % XXX
	    g_env$putl(e, ".tf) {")
	    g_env$indent(e)

	    tagcase each_arm.var		 % do we have a variable?
	       tag decl (d: decl):
		   var: idn := idnlist$bottom(d.idns) % there can be only one
		   val: expr := mexpr$value_one(tval) % value in the temporary
		   g_c_assn(e, var, val)
	       others:
	       end
	    g_body(e, each_arm.body)

	    g_env$putl(e, "break; /* tagcase */")
	    g_env$outdent(e)
	    g_env$putl(e, "}")
	    g_env$putl(e, "}")
	    end % each_arm

	% emit others arm if present
	tagcase stmnt.others_
	   tag body (bod: body):
	       g_env$putl(e, "{ /* others */")
	       g_env$indent(e)

	       g_body(e, bod)

	       g_env$outdent(e)
	       g_env$putl(e, "}")
	   others:
	   end

	g_env$putl(e, "} while (0); /* end tagcase */")
	end
       except when bounds: end
    end g_tagcase


g_return = proc (e: g_env, exp: exprlist)

    %% RETURN ( exp )

    g_env$putl(e, "{ /* return */")

    if ib$bodies()
       then
	    iterp: bool := current_apply$is_iter()
	    if iterp then
	       % an iter's iterbody
	       g_locals(e)
	       g_env$putl(e, "body_ctrl_req = true;")
	       g_env$putl(e, "signal(ERR_iteriterbodyreturn);")
	       else
		    % a proc's iterbody
		    i: int := 0
		    for each_e: expr in exprlist$elements(exp) do
			g_env$putl(e, "{")
			g_expr0(e, each_e)
			el: exprlist := g_expr1(e, each_e)
			g_sig_assn(e, i, exprlist$bottom(el))
			% g_env$puts(e, "elist["||int$unparse(i)||"].num = ")
			% ref to num 6/26/90
			% g_expr2(e, exprlist$bottom(el))
			% g_env$putl(e, ";")
			g_env$putl(e, "}")
			i := i + 1
			end % for
		    g_env$putl(e, "*iecode = ERR_ok;")
		    g_env$putl(e, "signal(ERR_iterbodyreturn);")
	       end
       else
	    % not in an iterbody, i.e. a normal return
	    if current_apply$is_iter() cand exprlist$size(exp) ~= 0 then
	       logit(802, "Non-empty return list for an iterator?!") end
	    i: int := 1
	    for each_e: expr in exprlist$elements(exp) do
		g_env$putl(e, "{")
		g_expr0(e, each_e)
		el: exprlist := g_expr1(e, each_e)
		g_ret_assn(e, i, exprlist$bottom(el))
		% g_env$puts(e, "ret_"||int$unparse(i)||"->num = ")
		% g_expr2(e, exprlist$bottom(el))
		% g_env$putl(e, ";")
		g_env$putl(e, "}")
		i := i + 1
		end % for
    	    g_env$putl(e, "signal (ERR_ok);")
       end
       except when bounds: end
    g_env$putl(e, "}")
    end g_return


g_yield = proc (e: g_env, exp: exprlist)

    %% YIELD ( exp )
    %%    occurs only in iterators (and their bodies)

    g_env$putl(e, "{ /* yield */")

    % declare temps we will need to compute yielded values
    for each_exp:expr in exprlist$elements(exp) do
	g_expr_arg0(e, each_exp)
	end

    % compute the values
    el:exprlist := exprlist$new()
    for each_exp: expr in exprlist$elements(exp) do
	new_el: exprlist := g_expr_arg1(e, each_exp)
	el := seq_extend[expr](el, new_el)
	end

    % call the caller's iterbody passed to us as "proc"
    g_err(e)
    g_env$puts(e, " = ")
    g_proc(e)
    g_env$puts(e, "(")
    % ... pass computed values to the iterbody
    for each_exp: expr in exprlist$elements(el) do
	g_expr_arg(e, each_exp)
	g_env$puts(e, ", ")
	end
    % ... also pass pointers to the caller's locals and ecode2
    g_user_locals(e)
    g_env$puts(e, ", ")
    g_env$putl(e, "iecode);")  % NO "&" here!

    if ib$bodies()
       then
	    % ITERATOR BODY:
	    %	attempt to process iteriterbodyexit (8/21/93 if bcr set!)
	    %	propagate everything else
	    % 5/31/90: probably should just propagate iteriterbodyexit
	    g_env$puts(e, "if (")
	    g_locals(e)
	    g_env$puts(e, "body_ctrl_req && ")
	    g_err(e)
	    g_env$putl(e, " == ERR_iteriterbodyexit) {")
	    g_env$indent(e)
	    g_err(e)
	    g_env$putl(e, " = *iecode;")
	    g_env$putl(e, "goto " || ex$get_current_ex_label() || ";")
	    g_env$outdent(e)
	    g_env$putl(e, "}")

	    % propagate other errors
	    g_env$puts(e, "if (")
	    g_err(e)
	    g_env$putl(e, " != ERR_ok) {")
	    g_env$indent(e)
	    g_env$puts(e, "signal(")
	    g_err(e)
	    g_env$putl(e, ");")
	    g_env$outdent(e)
	    g_env$putl(e, "}")
       else
	    % ITERATOR PROPER:
	    %	convert break to ok
	    %   7/1/93: removed conversion of break to ok: just resignal
	    %	propagate iterbodyreturn, iterbodysignal, iterbodyexit
	    %		i.e. pass through to invoker of iterator
	    % 5/31/90: removed irrelevant comments;
	    %	       propagated ibs directly (needed) (formerly signal(*iecode)
	    %	       propagated everything else (not *iecode) (?appropriate)
	    g_env$puts(e, "if (")
	    g_err(e)
	    g_env$putl(e, " != ERR_ok) {")
	    g_env$indent(e)

	    % % XXX: uwe: Don't spell these out separately when the
	    % % signal() at the end will propagate them anyway.  The
	    % % "revision control" comments above seem to suggest that
	    % % different calling convention was used for them.
	    % g_propagate(e, "ERR_break")
	    % g_propagate(e, "ERR_iterbodyreturn")
	    % g_propagate(e, "ERR_iterbodyexit")
	    % g_propagate(e, "ERR_iterbodysignal")
	    g_env$puts(e, "signal(")
	    g_err(e)
	    g_env$putl(e, ");")

	    g_env$outdent(e)
	    g_env$putl(e, "}")
       end
    g_env$putl(e, "}")
    end g_yield


g_signal = proc (e: g_env, cond: condition)

    %% SIGNAL cond.name ( cond.args )

    g_env$putl(e, "{ /* signal */")
    g_env$indent(e)

    % declare temps we will need to compute exception results
    for each_e: expr in exprlist$elements(cond.args) do
	g_expr0(e, each_e)
	end

    % compute and store exception results in elist[i]
    i: int := 0
    for each_e: expr in exprlist$elements(cond.args) do
	el: exprlist := g_expr1(e, each_e)
	g_sig_assn(e, i, exprlist$bottom(el))
	i := i + 1
	end

    % signal the exception to the caller
    sig_name: string
    if ~ib$bodies()
       then
	    sig_name := sig$name(cond.name)
       else
	    g_env$putl(e, "*iecode = " || sig$expr(cond.name) || ";")
	    if current_apply$is_iter()
	       then
		    g_locals(e)
		    g_env$putl(e, "body_ctrl_req = true;")
		    sig_name := "ERR_iteriterbodysignal";
	       else
		    sig_name := "ERR_iterbodysignal"
	       end
       end
    g_env$putl(e, "signal(" || sig_name || ");")

    g_env$outdent(e)
    g_env$putl(e, "}")
    end g_signal

g_exit = proc (e: g_env, cond: condition)

    %% EXIT cond.name ( cond.args )

    g_env$putl(e, "{ /* exit */")
    g_env$indent(e)

    % declare temps we will need to compute exception results
    for each_e: expr in exprlist$elements(cond.args) do
	g_expr0(e, each_e)
	end

    % compute and store exception results in elist[i]
    % XXX: uwe: why is this different from g_signal?  An oversight?
    % Cf. commented out old code in g_return for iterbodies
    el: exprlist := exprlist$new()
    for each_e: expr in exprlist$elements(cond.args) do
	new_el: exprlist := g_expr1(e, each_e)
	el := seq_extend[expr](el, new_el)
	end
    i: int := 0
    for each_e: expr in exprlist$elements(el) do
	g_env$puts(e, "elist[" || int$unparse(i) || "].num = ")
	g_expr2(e, each_e)
	g_env$putl(e, ";")
	i := i + 1
	end

    % signal the exception to a local handler
    g_err(e)
    g_env$putl(e, " = " || sig$expr(cond.name) || ";")
    g_env$putl(e, "goto " || ex$get_current_ex_label() || ";")

    g_env$outdent(e)
    g_env$putl(e, "}")
    end g_exit

g_break = proc (e: g_env)

    %% BREAK

    if loop_context$is_while()
       then
	    n: int := loop_context$get_current_while_count()
	    g_env$putl(e, "goto end_while_" || int$unparse(n) || ";")
     elseif loop_context$is_inline_for()
       then
	    n: int := loop_context$get_current_inline_for_count()
	    g_env$putl(e, "goto end_inline_for_" || int$unparse(n) || ";")
     else
	  iterp: bool := current_apply$is_iter()
	  if ib$bodies() cand iterp then
	     % 8/1/93: added body_ctrl_req clause
	     g_locals(e)
	     g_env$putl(e, "body_ctrl_req = true;")
	     end
	  g_env$putl(e, "signal(ERR_break);")
     end
    end g_break

g_continue = proc (e: g_env)

    %% CONTINUE

    if loop_context$is_while() cor loop_context$is_inline_for()
       then
	    g_env$putl(e, "continue;")
       else
	    g_env$putl(e, "signal(ERR_ok); /* continue */")
       end
    end g_continue

g_beginend = proc (e: g_env, bod: body)

    %% BEGIN
    %% bod.stmts
    %% END
    % (can leave out BEGIN END if only 1 stmnt, otherwise might need it if
    % this body has attached exception handlers)

    g_env$putl(e, "{ /* begin */")
    g_env$indent(e)
    g_body(e, bod)
    g_env$outdent(e)
    g_env$putl(e, "}")
    end g_beginend

g_body = proc (e: g_env, bod: body)
    %% bod.stmts
    % (can ignore equates)

    for s: stmt in stmtlist$elements(bod.stmts) do
	g_stmt(e, s)
	end
    end g_body

g_except = proc (e: g_env, stmnt: exceptstmt)

    %% stmnt.stmt
    %%    EXCEPT WHEN stmnt.arms[1]
    %%		  :
    %%		 WHEN stmnt.arms[n]
    %%		 OTHERS ( stmnt.others_.arm.decl) :
    %%			stmnt.others_.arm.body
    %%	  END

    ex$enter()
    g_stmt(e, stmnt.stmt)

    el: string := ex$get_current_end_label()
    g_env$putl(e, "goto " || el || ";")

    g_label(e, ex$get_current_ex_label())
    g_env$putl(e, " /* except */")
    g_env$putl(e, "__CLU_EX_HANDLER;")
    ex$leave()

    melse: string := ""
    align_indent: string := "    "	 % ^if (^)

    for h: handler in handlerlist$elements(stmnt.arms) do

	% [else] if (conditions ...)
	g_env$puts(e, melse)
	g_env$puts(e, "if (")

	align: string := ""
	one_line: bool := true
	for each_name: string in namelist$elements(h.names) do
	    if ~string$empty(align) then
	       one_line := false
	       g_env$newline(e)
	       g_env$puts(e, align)
	       g_env$puts(e, "|| ")
	       end
	    g_errcmp(e, each_name)
	    align := align_indent
	    end
	g_env$puts(e, ")")
	if one_line then g_env$puts(e, " ")
	   else g_env$newline(e) end
	g_env$putl(e, "{")
	g_env$indent(e)

	melse := "else "
	align_indent := "         "	 % ^else if (^)

	% assign exception results
	tagcase h.vars
	   tag decls (dlist: decllist):
	       i: int := 0
	       for each_d: decl in decllist$elements(dlist) do
		   for each_idn: idn in idnlist$elements(each_d.idns) do
		       g_idn(e, each_idn)
		       g_env$puts(e, " = elist["
				       || int$unparse(i) || "].num") % ref to num 6/26/90
		       g_env$putl(e, ";")
		       i := i + 1
		       end
		   end
	   tag star:
	   others:
	   end

	g_body(e, h.body)
	g_env$ensure_newline(e)
	g_env$outdent(e)
	g_env$putl(e, "}")
	end
    % [else] others/unhandled
    g_env$puts(e, melse)
    g_env$puts(e, "{")
    g_env$indent(e)
    if mothersarm$is_arm(stmnt.others_)
       then arm: othersarm := mothersarm$value_arm(stmnt.others_)
	    g_env$putl(e, " /* others */")
	    if mdecl$is_decl(arm.decl)
	       then
		    d: decl := mdecl$value_decl(arm.decl)
		    % 3/14/94: dcurtis: handle variable properly for others:
		    %		at most one var; set it to signal name
		    g_idn_plain(e, idnlist$bottom(d.idns))
		    g_env$puts(e, " = _pclu_erstr(")
	            g_err(e)
		    g_env$putl(e, ");")
	       end
	    g_env$newline(e)
	    g_body(e, arm.body)
       else
	    g_env$putl(e, " /* not handled */")
	    g_env$putl(e, "goto " || ex$get_current_ex_label() || ";")
       end
    g_env$outdent(e)
    g_env$putl(e, "}")
    g_env$newline(e)

    g_label(e, el)
    % not marked with __CLU_END_LABEL b/c we emit the goto just above
    g_env$putl(e, ";")
    end g_except


g_resignal = proc (e: g_env, stmnt: resignalstmt)

    %% stmnt.stmt
    %%     RESIGNAL stmnt.resigs

    % enter an exception handling scope
    ex$enter()

    % generate normal statements within that scope
    g_stmt(e, stmnt.stmt)

    % generate exception handlers
    % first let normal code hop around the handlers
    el: string := ex$get_current_end_label()
    % the following line was uncommented 12/4 (other problems may recur?)
    g_env$putl(e, "goto " || el || ";")

    % emit exception handlers' label
    g_label(e, ex$get_current_ex_label())
    g_env$putl(e, " /* resignal */")
    g_env$putl(e, "__CLU_EX_HANDLER;")

    % let further signals be caught by surrounding scope
    ex$leave()

    % emit a nested if statement for each possible signal and
    %	generate a signal for it; (results are already sitting in elist)
    melse: string := ""
    for each_sig: name in namelist$elements(stmnt.resigs) do
	g_env$puts(e, melse)
	melse := "else "
	g_env$puts(e, "if (")
	g_errcmp(e, each_sig)
	g_env$puts(e, ")")
	if ib$bodies()
	   then
		g_env$putl(e, " {");
		g_env$indent(e)
		g_env$puts(e, "*iecode = ")
		g_err(e)
		g_env$putl(e, ";")
		if current_apply$is_iter()
		   then
			g_locals(e)
			g_env$putl(e, "body_ctrl_req = true;")
			g_env$putl(e, "signal(ERR_iteriterbodysignal);")

		   else
			g_env$putl(e, "signal(ERR_iterbodysignal);")
		   end
		g_env$outdent(e)
		g_env$putl(e, "}")
	   else
		g_env$newline(e)
		g_env$indent(e)
		g_env$puts(e, "signal(");
		g_err(e)
		g_env$putl(e, ");")
		g_env$outdent(e)
	   end
	end

    % send other signals to surrounding handler
    g_env$putl(e, "else")
    g_env$indent(e)
    g_env$putl(e, "goto " || ex$get_current_ex_label() || ";")
    g_env$outdent(e)

    % emit end of statement and exception handler label
    % not marked with __CLU_END_LABEL b/c we emit the goto just above
    g_label(e, el)
    g_env$putl(e, ";")
    end g_resignal
